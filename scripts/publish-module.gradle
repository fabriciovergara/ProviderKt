apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    PUBLISH_GROUP_ID = 'dev.fabriciovergara'
    PUBLISH_VERSION = '0.14.0'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
//                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'State management for compose'
                    url = 'https://github.com/fabriciovergara/ProviderKt'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
                            id = 'fabriciovergaral'
                            name = 'Fabricio Vergara'
                            email = 'fabriciovergal@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/fabriciovergaral/ProviderKt.git'
                        developerConnection = 'scm:git:ssh://github.com/fabriciovergaral/ProviderKt.git'
                        url = 'https://github.com/fabriciovergaral/ProviderKt/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
